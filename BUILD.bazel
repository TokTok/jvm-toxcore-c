load("@protobuf//bazel:cc_proto_library.bzl", "cc_proto_library")
load("@rules_cc//cc:defs.bzl", "cc_binary")
load("@rules_java//java:defs.bzl", "java_proto_library")
load("@rules_kotlin//kotlin:jvm.bzl", "kt_jvm_library", "kt_jvm_test")
load("@rules_kotlin//kotlin:lint.bzl", "ktlint_fix", "ktlint_test")
load("@rules_proto//proto:defs.bzl", "proto_library")
load("//tools/project:build_defs.bzl", "project")

project()

genrule(
    name = "copy_link_jni_md_header",
    srcs = select({
        "@platforms//os:freebsd": ["@bazel_tools//tools/jdk:jni_md_header-freebsd"],
        "@platforms//os:linux": ["@bazel_tools//tools/jdk:jni_md_header-linux"],
        "@platforms//os:osx": ["@bazel_tools//tools/jdk:jni_md_header-darwin"],
        "@platforms//os:windows": ["@bazel_tools//tools/jdk:jni_md_header-windows"],
    }),
    outs = ["lib/src/jvmMain/cpp/jni_md.h"],
    cmd = "cp -f $< $@",
    tags = ["java"],
)

genrule(
    name = "copy_link_jni_header",
    srcs = ["@bazel_tools//tools/jdk:jni_header"],
    outs = ["lib/src/jvmMain/cpp/jni.h"],
    cmd = "cp -f $< $@",
    tags = ["java"],
)

proto_library(
    name = "jni_proto",
    srcs = [
        "lib/src/jvmMain/proto/Av.proto",
        "lib/src/jvmMain/proto/Core.proto",
    ],
    tags = ["java"],
)

cc_proto_library(
    name = "jni_cc_proto",
    tags = ["java"],
    deps = [":jni_proto"],
)

java_proto_library(
    name = "jni_java_proto",
    tags = ["java"],
    deps = [":jni_proto"],
)

cc_binary(
    name = "libtox4j-c.so",
    srcs = glob([
        "lib/src/jvmMain/cpp/**/*.cpp",
        "lib/src/jvmMain/cpp/**/*.h",
    ]) + [
        ":lib/src/jvmMain/cpp/jni.h",
        ":lib/src/jvmMain/cpp/jni_md.h",
    ],
    includes = [
        "lib/src/jvmMain/cpp",
        "lib/src/jvmMain/proto",
    ],
    linkopts = select({
        "@platforms//os:freebsd": ["-Wl,--version-script,$(location lib/src/jvmMain/cpp/libtox4j-c.ld)"],
        "@platforms//os:linux": ["-Wl,--version-script,$(location lib/src/jvmMain/cpp/libtox4j-c.ld)"],
        "@platforms//os:osx": [],
        "@platforms//os:windows": [],
    }),
    linkshared = True,
    tags = ["java"],
    visibility = ["//visibility:public"],
    deps = [
        "lib/src/jvmMain/cpp/libtox4j-c.ld",
        ":jni_cc_proto",
        "//c-toxcore",
        "@libsodium",
    ],
)

kt_jvm_library(
    name = "jvm-toxcore-c",
    srcs = glob([
        "lib/src/*Main/**/*.java",
        "lib/src/*Main/**/*.kt",
    ]),
    data = ["libtox4j-c.so"],
    tags = ["java"],
    visibility = ["//visibility:public"],
    deps = [":jni_java_proto"],
)

kt_jvm_test(
    name = "ToxCoreTest",
    size = "small",
    srcs = glob(["lib/src/jvmTest/**/*.kt"]),
    jvm_flags = ["-Djava.library.path=jvm-toxcore-c"],
    tags = ["java"],
    test_class = "im.tox.tox4j.core.ToxCoreTest",
    deps = [
        ":jvm-toxcore-c",
        "@maven//:org_jetbrains_kotlin_kotlin_test_junit",
        "@maven//:org_jetbrains_kotlinx_kotlinx_coroutines_core",
    ],
)

ktlint_fix(
    name = "ktlint_fix",
    srcs = glob(["**/*.kt"]),
)

ktlint_test(
    name = "ktlint_test",
    size = "small",
    srcs = glob(["**/*.kt"]),
    tags = ["java"],
)
