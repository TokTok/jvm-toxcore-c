load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_proto_library")
load("@rules_java//java:defs.bzl", "java_proto_library", "java_test")
load("@rules_kotlin//kotlin:jvm.bzl", "kt_jvm_library", "kt_jvm_test")
load("@rules_proto//proto:defs.bzl", "proto_library")
load("//tools/project:build_defs.bzl", "project")

project()

genrule(
    name = "copy_link_jni_md_header",
    srcs = select({
        "//tools/config:freebsd": ["@bazel_tools//tools/jdk:jni_md_header-freebsd"],
        "//tools/config:linux": ["@bazel_tools//tools/jdk:jni_md_header-linux"],
        "//tools/config:osx": ["@bazel_tools//tools/jdk:jni_md_header-darwin"],
        "//tools/config:windows": ["@bazel_tools//tools/jdk:jni_md_header-windows"],
    }),
    outs = ["cpp/src/jni_md.h"],
    cmd = "cp -f $< $@",
)

genrule(
    name = "copy_link_jni_header",
    srcs = ["@bazel_tools//tools/jdk:jni_header"],
    outs = ["cpp/src/jni.h"],
    cmd = "cp -f $< $@",
)

proto_library(
    name = "jni_proto",
    srcs = [
        "src/main/protobuf/Av.proto",
        "src/main/protobuf/Core.proto",
        "src/main/protobuf/ProtoLog.proto",
    ],
)

cc_proto_library(
    name = "jni_cc_proto",
    deps = [":jni_proto"],
)

java_proto_library(
    name = "jni_java_proto",
    deps = [":jni_proto"],
)

cc_binary(
    name = "libtox4j-c.so",
    srcs = glob([
        "cpp/src/**/*.cpp",
        "cpp/src/**/*.h",
    ]) + [
        ":cpp/src/jni.h",
        ":cpp/src/jni_md.h",
    ],
    copts = [
        "-DHAVE_TO_STRING",
    ],
    includes = [
        "cpp/src",
        "src/main/protobuf",
    ],
    linkopts = select({
        "//tools/config:freebsd": ["-Wl,--version-script,$(location cpp/src/libtox4j-c.ld)"],
        "//tools/config:linux": ["-Wl,--version-script,$(location cpp/src/libtox4j-c.ld)"],
        "//tools/config:osx": [],
        "//tools/config:windows": [],
    }),
    linkshared = True,
    deps = [
        "cpp/src/libtox4j-c.ld",
        ":jni_cc_proto",
        "//c-toxcore",
    ],
)

kt_jvm_library(
    name = "jvm-toxcore-c",
    srcs = glob([
        "src/main/java/**/*.java",
        "src/main/java/**/*.kt",
    ]),
    data = ["libtox4j-c.so"],
    deps = [":jni_java_proto"],
)

kt_jvm_test(
    name = "ToxCoreTest",
    size = "small",
    srcs = ["src/test/java/im/tox/tox4j/core/ToxCoreTest.kt"],
    jvm_flags = ["-Djava.library.path=jvm-toxcore-c"],
    test_class = "im.tox.tox4j.core.ToxCoreTest",
    deps = [
        ":jvm-toxcore-c",
        "@maven//:org_jetbrains_kotlin_kotlin_test_junit",
        "@maven//:org_jetbrains_kotlinx_kotlinx_coroutines_core",
    ],
)

java_test(
    name = "ToxCoreJavaTest",
    size = "small",
    srcs = ["src/test/java/im/tox/tox4j/core/ToxCoreJavaTest.java"],
    jvm_flags = ["-Djava.library.path=jvm-toxcore-c"],
    test_class = "im.tox.tox4j.core.ToxCoreJavaTest",
    deps = [
        ":jvm-toxcore-c",
        "@maven//:junit_junit",
    ],
)
