load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_proto_library")
load("@rules_java//java:defs.bzl", "java_test")
load("@io_bazel_rules_scala//scala:scala.bzl", "scala_binary", "scala_library", "scala_test")
load("@io_bazel_rules_scala//scala_proto:scala_proto.bzl", "scalapb_proto_library")
load("@rules_proto//proto:defs.bzl", "proto_library")
load("//tools/project:build_defs.bzl", "project")

project()

# If you are on Windows, the relative path below doesn't work, so you'll need
# to use an absolute path like below. If you find a way to make this work with
# relative paths, please send a pull request.
USER = "iphydf"

JVM_FLAGS = select({
    "//tools/config:windows": ["-Djava.library.path=C:/Users/%s/source/repos/toktok-stack/bazel-bin/jvm-toxcore-c" % USER],
    "//conditions:default": ["-Djava.library.path=jvm-toxcore-c"],
})

genrule(
    name = "copy_link_jni_md_header",
    srcs = select({
        "//tools/config:freebsd": ["@bazel_tools//tools/jdk:jni_md_header-freebsd"],
        "//tools/config:linux": ["@bazel_tools//tools/jdk:jni_md_header-linux"],
        "//tools/config:osx": ["@bazel_tools//tools/jdk:jni_md_header-darwin"],
        "//tools/config:windows": ["@bazel_tools//tools/jdk:jni_md_header-windows"],
    }),
    outs = ["cpp/src/jni_md.h"],
    cmd = "cp -f $< $@",
)

genrule(
    name = "copy_link_jni_header",
    srcs = ["@bazel_tools//tools/jdk:jni_header"],
    outs = ["cpp/src/jni.h"],
    cmd = "cp -f $< $@",
)

proto_library(
    name = "jni_proto",
    srcs = [
        "src/main/protobuf/Av.proto",
        "src/main/protobuf/Core.proto",
        "src/main/protobuf/ProtoLog.proto",
    ],
)

cc_proto_library(
    name = "jni_cc_proto",
    deps = [":jni_proto"],
)

scalapb_proto_library(
    name = "jni_scala_proto",
    deps = [":jni_proto"],
)

cc_binary(
    name = "libtox4j-c.so",
    srcs = glob([
        "cpp/src/**/*.cpp",
        "cpp/src/**/*.h",
    ]) + [
        ":cpp/src/jni.h",
        ":cpp/src/jni_md.h",
    ],
    copts = [
        "-DHAVE_TO_STRING",
    ],
    includes = [
        "cpp/src",
        "src/main/protobuf",
    ],
    linkopts = select({
        "//tools/config:freebsd": ["-Wl,--version-script,$(location cpp/src/libtox4j-c.ld)"],
        "//tools/config:linux": ["-Wl,--version-script,$(location cpp/src/libtox4j-c.ld)"],
        "//tools/config:osx": [],
        "//tools/config:windows": [],
    }),
    linkshared = True,
    deps = [
        "cpp/src/libtox4j-c.ld",
        ":jni_cc_proto",
        "//c-toxcore",
    ],
)

genrule(
    name = "native_macho",
    srcs = [":libtox4j-c.so"],
    outs = ["libtox4j-c.dylib"],
    cmd = "cp $< $@",
)

genrule(
    name = "native_windows",
    srcs = [":libtox4j-c.so"],
    outs = ["tox4j-c.dll"],
    cmd = "cp $< $@",
)

filegroup(
    name = "native",
    srcs = select({
        "//tools/config:freebsd": [":libtox4j-c.so"],
        "//tools/config:linux": [":libtox4j-c.so"],
        "//tools/config:osx": [":libtox4j-c.dylib"],
        "//tools/config:windows": [":tox4j-c.dll"],
    }),
    visibility = ["//visibility:public"],
)

scala_library(
    name = "jvm-toxcore-c",
    srcs = glob([
        "src/main/java/**/*.java",
        "src/main/java/**/*.scala",
    ]),
    visibility = ["//visibility:public"],
    deps = [
        ":jni_scala_proto",
        "//jvm-macros",
        "//jvm-toxcore-api",
        "@maven//:com_google_guava_guava",
        "@maven//:com_typesafe_scala_logging_scala_logging_2_11",
        "@maven//:org_jetbrains_annotations",
        "@maven//:org_slf4j_slf4j_api",
    ],
)

scala_library(
    name = "codegen_lib",
    testonly = True,
    srcs = glob([
        "src/test/java/gnieh/**/*.scala",
        "src/test/java/im/tox/tox4j/impl/jni/codegen/**/*.scala",
    ]) + [
        "src/test/java/im/tox/tox4j/impl/jni/MethodMap.scala",
    ],
    deps = [
        ":jni_scala_proto",
        ":jvm-toxcore-c",
        "//jvm-macros",
        "//jvm-toxcore-api",
        "@maven//:com_google_guava_guava",
        "@maven//:org_apache_commons_commons_lang3",
        "@maven//:org_jetbrains_annotations",
    ],
)

scala_library(
    name = "test_lib",
    testonly = True,
    srcs = [
        "src/test/java/im/tox/core/random/RandomCore.scala",
        "src/test/java/im/tox/tox4j/ConnectedListener.scala",
        "src/test/java/im/tox/tox4j/DhtNode.scala",
        "src/test/java/im/tox/tox4j/DhtNodeSelector.scala",
        "src/test/java/im/tox/tox4j/SocksServer.scala",
        "src/test/java/im/tox/tox4j/TestConstants.scala",
        "src/test/java/im/tox/tox4j/ToxCoreTestBase.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/audio/AudioGenerator.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/audio/AudioGenerators.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/audio/AudioPlayback.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/video/ArithmeticVideoGenerator.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/video/RgbVideoGenerator.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/video/TextImageGenerator.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/video/VideoConversions.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/video/VideoGenerator.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/video/VideoGenerators.scala",
        "src/test/java/im/tox/tox4j/core/SmallNat.scala",
        "src/test/java/im/tox/tox4j/core/ToxCoreFactory.scala",
        "src/test/java/im/tox/tox4j/core/ToxList.scala",
        "src/test/java/im/tox/tox4j/core/callbacks/FilePauseResumeTestBase.scala",
        "src/test/java/im/tox/tox4j/core/callbacks/InvokeTest.scala",
        "src/test/java/im/tox/tox4j/crypto/ToxCryptoTest.scala",
        "src/test/java/im/tox/tox4j/impl/jni/NamingConventionsTest.scala",
        "src/test/java/im/tox/tox4j/impl/jni/ToxAvImplFactory.scala",
        "src/test/java/im/tox/tox4j/impl/jni/ToxCoreImplFactory.scala",
        "src/test/java/im/tox/tox4j/testing/GetDisjunction.scala",
        "src/test/java/im/tox/tox4j/testing/ToxExceptionChecks.scala",
        "src/test/java/im/tox/tox4j/testing/ToxTestMixin.scala",
        "src/test/java/im/tox/tox4j/testing/autotest/AliceBobTest.scala",
        "src/test/java/im/tox/tox4j/testing/autotest/AliceBobTestBase.scala",
        "src/test/java/im/tox/tox4j/testing/autotest/AutoTest.scala",
        "src/test/java/im/tox/tox4j/testing/autotest/AutoTestSuite.scala",
        "src/test/java/im/tox/tox4j/testing/autotest/ChatClient.scala",
    ],
    deps = [
        ":codegen_lib",
        ":jvm-toxcore-c",
        "//jvm-macros",
        "//jvm-toxcore-api",
        "@io_bazel_rules_scala//scala/scalatest",
        "@maven//:com_chuusai_shapeless_2_11",
        "@maven//:com_typesafe_scala_logging_scala_logging_2_11",
        "@maven//:org_jetbrains_annotations",
        "@maven//:org_scalacheck_scalacheck_2_11",
        "@maven//:org_slf4j_slf4j_api",
    ],
)

[scala_test(
    name = src[src.rindex("/") + 1:-6],
    size = "small",
    srcs = [src],
    data = [":native"],
    jvm_flags = JVM_FLAGS,
    resources = glob([
        "src/test/resources/**/*",
    ]),
    runtime_deps = [
        "@maven//:log4j_log4j",
        "@maven//:org_slf4j_slf4j_log4j12",
    ],
    deps = [
        ":codegen_lib",
        ":jni_scala_proto",
        ":jvm-toxcore-c",
        ":test_lib",
        "//jvm-macros",
        "//jvm-toxcore-api",
        "@maven//:com_chuusai_shapeless_2_11",
        "@maven//:com_google_guava_guava",
        "@maven//:com_typesafe_scala_logging_scala_logging_2_11",
        "@maven//:org_apache_commons_commons_lang3",
        "@maven//:org_jetbrains_annotations",
        "@maven//:org_scalacheck_scalacheck_2_11",
        "@maven//:org_slf4j_slf4j_api",
    ],
) for src in glob(["src/test/java/**/*Test.scala"])]

[java_test(
    name = src[src.rindex("/") + 1:-5],
    size = "small",
    srcs = [src],
    data = [":native"],
    jvm_flags = JVM_FLAGS,
    resources = glob([
        "src/test/resources/**/*",
    ]),
    runtime_deps = [
        "@maven//:log4j_log4j",
        "@maven//:org_slf4j_slf4j_log4j12",
    ],
    deps = [
        ":jvm-toxcore-c",
        "//jvm-toxcore-api",
        "@io_bazel_rules_scala//scala/scalatest",
        "@maven//:junit_junit",
        "@maven//:org_slf4j_slf4j_api",
    ],
) for src in glob(["src/test/java/**/*.java"])]

[scala_binary(
    name = src[src.rindex("/") + 1:-6],
    testonly = True,
    srcs = [src],
    main_class = "im.tox.tox4j.impl.jni.codegen." + src[src.rindex("/") + 1:-6],
    resources = glob([
        "src/test/resources/**/*",
    ]),
    deps = [
        ":codegen_lib",
        ":jni_scala_proto",
        ":jvm-toxcore-c",
        "//jvm-toxcore-api",
        "@maven//:com_google_guava_guava",
        "@maven//:org_jetbrains_annotations",
    ],
) for src in glob(["src/test/java/im/tox/tox4j/impl/jni/codegen/Jni*.scala"])]

scala_binary(
    name = "AudioPlaybackShow",
    testonly = True,
    srcs = [
        "src/test/java/im/tox/tox4j/av/callbacks/audio/AudioGenerator.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/audio/AudioGenerators.scala",
        "src/test/java/im/tox/tox4j/av/callbacks/audio/AudioPlayback.scala",
        "src/tools/java/im/tox/tox4j/av/callbacks/audio/AudioPlaybackShow.scala",
    ],
    main_class = "im.tox.tox4j.av.callbacks.audio.AudioPlaybackShow",
    resources = glob([
        "src/test/resources/**/*",
    ]),
    deps = [
        "//jvm-toxcore-api",
    ],
)

scala_test(
    name = "AudioReceiveFrameCallbackShow",
    size = "small",
    srcs = ["src/tools/java/im/tox/tox4j/av/callbacks/audio/AudioReceiveFrameCallbackShow.scala"],
    data = [":native"],
    #jvm_flags = JVM_FLAGS,
    resources = glob([
        "src/test/resources/**/*",
    ]),
    tags = ["manual"],
    runtime_deps = [
        "@maven//:log4j_log4j",
        "@maven//:org_slf4j_slf4j_log4j12",
    ],
    deps = [
        ":jvm-toxcore-c",
        ":test_lib",
        "//jvm-toxcore-api",
        "@io_bazel_rules_scala//scala/scalatest",
        "@maven//:com_typesafe_scala_logging_scala_logging_2_11",
        "@maven//:org_jetbrains_annotations",
        "@maven//:org_slf4j_slf4j_api",
    ],
)

scala_test(
    name = "VideoReceiveFrameCallbackShow",
    size = "small",
    srcs = [
        "src/tools/java/im/tox/tox4j/av/callbacks/video/ConsoleVideoDisplay.scala",
        "src/tools/java/im/tox/tox4j/av/callbacks/video/GuiVideoDisplay.scala",
        "src/tools/java/im/tox/tox4j/av/callbacks/video/RgbVideoDisplay.scala",
        "src/tools/java/im/tox/tox4j/av/callbacks/video/VideoDisplay.scala",
        "src/tools/java/im/tox/tox4j/av/callbacks/video/VideoReceiveFrameCallbackShow.scala",
    ],
    data = [":native"],
    jvm_flags = JVM_FLAGS,
    resources = glob([
        "src/test/resources/**/*",
    ]),
    tags = ["manual"],
    runtime_deps = [
        "@maven//:log4j_log4j",
        "@maven//:org_slf4j_slf4j_log4j12",
    ],
    deps = [
        ":jvm-toxcore-c",
        ":test_lib",
        "//jvm-toxcore-api",
        "@io_bazel_rules_scala//scala/scalatest",
        "@maven//:com_chuusai_shapeless_2_11",
        "@maven//:com_typesafe_scala_logging_scala_logging_2_11",
        "@maven//:org_jetbrains_annotations",
        "@maven//:org_scala_lang_modules_scala_swing_2_11",
        "@maven//:org_slf4j_slf4j_api",
    ],
)
